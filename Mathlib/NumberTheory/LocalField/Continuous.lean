/-
Copyright (c) 2025 Andrew Yang. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Andrew Yang
-/
import Mathlib.FieldTheory.Finite.Basic
import Mathlib.NumberTheory.LocalField.Basic
import Mathlib.RingTheory.Henselian

/-!

# Any ring homomorphism between non-archimedean local fields are automatically continuous

-/

open ValuativeRel

variable {K : Type*} [Field K] [TopologicalSpace K] [ValuativeRel K] [IsNonarchimedeanLocalField K]

variable (K) in
noncomputable def residueChar : â„• := ringChar ğ“€[K]

instance : CharP ğ“€[K] (residueChar K) := inferInstanceAs (CharP _ (ringChar _))

lemma prime_residueChar : (residueChar K).Prime := by
  cases nonempty_fintype ğ“€[K]
  exact (FiniteField.card ğ“€[K] (ringChar ğ“€[K])).choose_spec.1

lemma exists_card_eq_residueChar_pow : âˆƒ f, Nat.card ğ“€[K] = residueChar K ^ f := by
  cases nonempty_fintype ğ“€[K]
  exact âŸ¨_, Fintype.card_eq_nat_card.symm.trans
    (FiniteField.card ğ“€[K] (ringChar ğ“€[K])).choose_spec.2âŸ©

lemma charZero_or_charP_residueChar : CharZero K âˆ¨ CharP K (residueChar K) := by
  have : CharP ğ“€[K] (residueChar K) := inferInstance
  obtain âŸ¨p, hpâŸ© := CharP.exists ğ’ª[K]
  obtain rfl | hp0 := eq_or_ne p 0
  Â· have : CharZero ğ’ª[K] := CharP.charP_to_charZero _
    exact .inl (charZero_of_injective_algebraMap (R := ğ’ª[K]) Subtype.val_injective)
  Â· have := CharP.of_ringHom_of_ne_zero (algebraMap ğ’ª[K] ğ“€[K]) p hp0
    obtain rfl : p = residueChar K := (CharP.existsUnique ğ“€[K]).unique â€¹_â€º â€¹_â€º
    exact .inr (CharP.of_ringHom_of_ne_zero (algebraMap ğ’ª[K] K) _ hp0)

scoped[ValuativeRel] notation3 "q[" K "]" => Nat.card ğ“€[K]
scoped[ValuativeRel] notation3 "p[" K "]" => residueChar K

lemma HenselianLocalRing.of_henselianRing (R : Type*) [CommRing R] [IsLocalRing R]
    [HenselianRing R (IsLocalRing.maximalIdeal R)] : HenselianLocalRing R where
  is_henselian f hâ‚ hâ‚‚ hâ‚ƒ hâ‚„ := HenselianRing.is_henselian f hâ‚ hâ‚‚ hâ‚ƒ (hâ‚„.map _)

instance : HenselianLocalRing ğ’ª[K] := .of_henselianRing _

open Polynomial in
lemma exists_pow_eq_pow_card_residueField_sub_one_of_valuation_eq_one
    {x : K} (hx : valuation K x = 1) {n : â„•} (hn : Â¬ p[K] âˆ£ n) :
    âˆƒ y, y ^ n = x ^ (q[K] - 1) := by
  have h1x : 1 â‰¤áµ¥ x := (valuation K).one_rel_iff.mpr (by simp [hx])
  have hx1 : x â‰¤áµ¥ 1 := (valuation K).rel_one_iff.mpr (by simp [hx])
  obtain âŸ¨x, rflâŸ© : âˆƒ y : ğ’ª[K], x = y := âŸ¨âŸ¨x, mem_integers_iff.mpr hx1âŸ©, rflâŸ©
  have : IsUnit (n : ğ’ª[K]) := by
    rwa [â† IsLocalRing.residue_ne_zero_iff_isUnit, map_natCast, ne_eq, CharP.cast_eq_zero_iff]
  obtain âŸ¨a, ha, -âŸ© := HenselianLocalRing.is_henselian (X ^ n - C (x ^ (q[K] - 1)))
    (monic_X_pow_sub_C _ (by aesop)) 1 (by
      cases nonempty_fintype ğ“€[K]
      have H : IsLocalRing.residue _ x â‰  0 := by
        rwa [ne_eq, IsLocalRing.residue_eq_zero_iff, mem_maximalIdeal_iff, srel_iff, not_not]
      rw [â† IsLocalRing.residue_eq_zero_iff, â† @Fintype.card_eq_nat_card ğ“€[K] inferInstance]
      simp [FiniteField.pow_card_sub_one_eq_one, H]) (by simpa [derivative_X_pow, - map_pow])
  exact âŸ¨a, by simpa [sub_eq_zero] using ha.map (f := algebraMap ğ’ª[K] K)âŸ©

open Polynomial in
lemma valuation_eq_one_iff {x : K} :
    valuation K x = 1 â†” x â‰  0 âˆ§ âˆƒ (p q : â„•), 1 < p âˆ§ q â‰  0 âˆ§ âˆ€ n, Â¬ p âˆ£ n â†’ âˆƒ y, y ^ n = x ^ q := by
  constructor
  Â· intro H
    exact âŸ¨by aesop, p[K], _, prime_residueChar.one_lt, (Nat.sub_pos_of_lt Finite.one_lt_card).ne',
      fun _ â†¦ exists_pow_eq_pow_card_residueField_sub_one_of_valuation_eq_one HâŸ©
  Â· rintro âŸ¨hxâ‚€, p, q, hp, hq, HâŸ©
    rw [le_antisymm_iff, â† (valuation K).rel_one_iff, â† (valuation K).one_rel_iff]
    wlog hxâ‚ : x â‰¤áµ¥ 1 generalizing x
    Â· have := @this xâ»Â¹ (by simpa) (fun n hn â†¦ by obtain âŸ¨a, haâŸ© := H n hn; exact âŸ¨aâ»Â¹, by simpaâŸ©)
        ((inv_rel_one hxâ‚€).mpr (SRel.rel hxâ‚))
      rwa [inv_rel_one hxâ‚€, one_rel_inv hxâ‚€, and_comm] at this
    rw [and_iff_right hxâ‚, â† not_srel_iff]
    intro hxâ‚
    obtain âŸ¨n, hn : _ < valuation K xâŸ© :=
      exists_pow_ltâ‚€ (uniformizer_lt_one (R := K)) ((Units.mk0 x hxâ‚€).map (valuation K))
    obtain âŸ¨a, haâŸ© := H (p * (q * n) + 1) (by rw [â† Nat.dvd_add_iff_right (by simp)]; aesop)
    by_cases ha1 : 1 â‰¤áµ¥ a
    Â· have := pow_rel_pow ha1 (p * (q * n) + 1)
      rw [one_pow, ha] at this
      exact absurd this (by simpa using pow_srel_pow hxâ‚ q hq)
    have :=
      calc uniformizer K ^ (q * n)
      _ â‰¤ valuation K x ^ q := by rw [pow_mul']; gcongr; simp
      _ = valuation K a ^ (p * (q * n) + 1) := by simp only [â† map_pow, ha]
      _ â‰¤ uniformizer K ^ (p * (q * n) + 1) := by
          have := le_uniformizer_iff.mpr ((valuation K).srel_one_iff.mp ha1); gcongr; simp
    apply lt_irrefl (p * (q * n) + 1)
    rw [pow_le_pow_iff_right_of_lt_oneâ‚€ uniformizer_pos uniformizer_lt_one] at this
    calc
    _ â‰¤ 1 * (q * n) := by simpa only [one_mul] using this
    _ â‰¤ p * (q * n) := by gcongr
    _ < _ := Nat.lt_succ_self _

variable {L : Type*} [Field L] [TopologicalSpace L] [ValuativeRel L] [IsNonarchimedeanLocalField L]

lemma valuation_map_eq_one
    (f : K â†’+* L) {x : K} (hx : valuation K x = 1) : valuation L (f x) = 1 := by
  rw [valuation_eq_one_iff] at hx âŠ¢
  obtain âŸ¨hx0, p, q, hp, hq, HâŸ© := hx
  refine âŸ¨by simpa, p, q, hp, hq, fun n hn â†¦ ?_âŸ©
  obtain âŸ¨y, hyâŸ© := H n hn
  refine âŸ¨f y, by simp only [â† map_pow, hy]âŸ©

lemma map_rel_one (f : K â†’+* L) {x : K} (hx : x â‰¤áµ¥ 1) : f x â‰¤áµ¥ 1 := by
  rw [(valuation _).rel_one_iff] at hx âŠ¢
  by_contra! hx'
  replace hx : valuation K x < 1 := lt_of_le_of_ne hx (mt (valuation_map_eq_one f) hx'.ne')
  have hâ‚ := valuation_map_eq_one f ((valuation K).map_one_add_of_lt hx)
  have hâ‚‚ := (valuation L).map_add_eq_of_lt_left ((map_one _).trans_lt hx')
  rw [map_add, map_one, add_comm, hâ‚‚] at hâ‚
  exact hx'.ne' hâ‚

lemma map_srel_one (f : K â†’+* L) {x : K} (hx : x <áµ¥ 1) : f x <áµ¥ 1 := by
  let fo : ğ’ª[K] â†’+* ğ’ª[L] := f.restrict _ _ (fun _ â†¦
    by simpa only [ValuativeRel.mem_integers_iff] using map_rel_one f)
  have : ((IsLocalRing.maximalIdeal ğ’ª[L]).comap fo).IsMaximal := by
    rw [Ideal.Quotient.maximal_ideal_iff_isField_quotient]
    exact @Finite.isField_of_domain _ _ _ (.of_injective (Î² := ğ“€[L]) _ Ideal.quotientMap_injective)
  have := ((IsLocalRing.isMaximal_iff _).mp this).ge (ValuativeRel.mem_maximalIdeal_iff
    (x := âŸ¨x, ValuativeRel.mem_integers_iff.mpr hx.relâŸ©).mpr hx)
  exact ValuativeRel.mem_maximalIdeal_iff.mp this

instance (f : K â†’+* L) : ((valuation L).comap f).Compatible where
  rel_iff_le {x y} := by
    by_cases hx : x = 0; Â· simp [hx]
    by_cases hy : y = 0; Â· simp [hy]
    simp only [Valuation.comap_apply, â† (valuation L).rel_iff_le]
    constructor
    Â· rw [â† div_rel_one_iff (by simpa), â† div_rel_one_iff (a := f x) (by simpa), â† map_divâ‚€]
      exact map_rel_one f
    Â· rw [â† one_rel_div_iff (by simpa), â† one_rel_div_iff (a := y) (by simpa), â† map_divâ‚€]
      intro hâ‚
      by_contra hâ‚‚
      exact map_srel_one f hâ‚‚ hâ‚

lemma comap_valuation_isEquiv (f : K â†’+* L) : ((valuation L).comap f).IsEquiv (valuation K) :=
  ValuativeRel.isEquiv _ _

lemma ringHom_continuous (f : K â†’+* L) : Continuous f := by
  refine continuous_of_continuousAt_zero _ ?_
  rw [ContinuousAt, map_zero, (IsValuativeTopology.hasBasis_nhds_zero' _).tendsto_iff
    (IsValuativeTopology.hasBasis_nhds_zero' _)]
  intro x hx
  obtain âŸ¨x, rflâŸ© := valuation_surjective x
  obtain âŸ¨y, hyâŸ© := valuation_surjective (uniformizer K)
  obtain âŸ¨n, hnâŸ© := exists_pow_ltâ‚€ ((comap_valuation_isEquiv f).lt_one_iff_lt_one.mpr
    (hy.trans_lt uniformizer_lt_one)) (.mk0 _ hx)
  refine âŸ¨valuation K y ^ n, pow_ne_zero _ (by simp [hy]), fun z hz â†¦ ?_âŸ©
  dsimp at hz âŠ¢
  rw [â† map_pow, â† (comap_valuation_isEquiv f).lt_iff_lt, map_pow] at hz
  exact hz.trans hn

instance [Algebra K L] : ValuativeExtension K L where
  rel_iff_rel x y := by
    rw [(valuation L).rel_iff_le, ((valuation L).comap (algebraMap _ _)).rel_iff_le]; rfl

instance [Algebra K L] : ContinuousSMul K L :=
  continuousSMul_of_algebraMap _ _ (ringHom_continuous _)

instance [Algebra K L] : Algebra ğ’ª[K] ğ’ª[L] where
  smul r s := âŸ¨(r : K) â€¢ s, Algebra.smul_def r.1 s.1 â–¸ mul_mem (ValuativeRel.mem_integers_iff.mpr
      (map_rel_one _ (ValuativeRel.mem_integers_iff.mp r.2))) s.2âŸ©
  algebraMap := (algebraMap K L).restrict _ _ fun _ â†¦ by
    simpa only [ValuativeRel.mem_integers_iff] using map_rel_one _
  commutes' _ _ := mul_comm _ _
  smul_def' r s := by ext; simp [â† Algebra.smul_def]; rfl

@[simp]
lemma coe_smul [Algebra K L] (r : ğ’ª[K]) (s : ğ’ª[L]) : â†‘(r â€¢ s) = (r : K) â€¢ (s : L) := rfl

@[simp]
lemma coe_algebraMap [Algebra K L] (r : ğ’ª[K]) : algebraMap ğ’ª[K] ğ’ª[L] r = algebraMap K L r := rfl

instance [Algebra K L] : IsLocalHom (algebraMap ğ’ª[K] ğ’ª[L]) :=
  ((IsLocalRing.local_hom_TFAE (algebraMap ğ’ª[K] ğ’ª[L])).out 3 0).mp <| by
    simp only [SetLike.le_def, Ideal.mem_comap, ValuativeRel.mem_maximalIdeal_iff,
      coe_algebraMap]
    exact fun _ â†¦ map_srel_one _

instance [Algebra K L] : MulSemiringAction (L â‰ƒâ‚[K] L) ğ’ª[L] where
  smul Ïƒ x := âŸ¨Ïƒ x, mem_integers_iff.mpr (map_rel_one Ïƒ.toRingHom (mem_integers_iff.mp x.2))âŸ©
  one_smul _ := rfl
  mul_smul _ _ _ := rfl
  smul_zero _ := Subtype.ext <| map_zero _
  smul_add _ _ _ := Subtype.ext <| map_add _ _ _
  smul_one _ := Subtype.ext <| map_one _
  smul_mul _ _ _ := Subtype.ext <| map_mul _ _ _

instance [Algebra K L] :
    SMulCommClass (L â‰ƒâ‚[K] L) ğ’ª[K] ğ’ª[L] where
  smul_comm Ïƒ _ _ := Subtype.ext <| map_smul Ïƒ _ _

open IsNonarchimedeanLocalField

lemma valuation_ringEquiv_eq_uniformizer
    (e : K â‰ƒ+* L) {x : K} (hx : valuation K x = uniformizer K) :
    valuation L (e x) = uniformizer L := by
  apply le_antisymm
  Â· rw [le_uniformizer_iff]
    refine ((valuation L).comap e.toRingHom).srel_one_iff.mp ((valuation K).srel_one_iff.mpr ?_)
    exact hx â–¸ uniformizer_lt_one
  Â· obtain âŸ¨y, hyâŸ© := valuation_surjective (uniformizer L)
    obtain âŸ¨y, rflâŸ© := e.surjective y
    rw [â† hy]
    refine ((valuation L).comap e.toRingHom).rel_iff_le.mp ((valuation K).rel_iff_le.mpr ?_)
    rw [hx, le_uniformizer_iff, â† (valuation K).srel_one_iff,
      ((valuation L).comap e.toRingHom).srel_one_iff]
    simpa [hy] using uniformizer_lt_one

lemma valueGroupWithZeroIsoInt_valuation_ringEquiv (e : K â‰ƒ+* L) (x : K) :
    valueGroupWithZeroIsoInt L (valuation L (e x)) =
      valueGroupWithZeroIsoInt K (valuation K x) := by
  by_cases hx : x = 0
  Â· simp [hx]
  obtain âŸ¨n, hnâŸ© := exists_uniformizer_zpow_eq (x := valuation K x) (by simpa)
  rw [â† hn]
  obtain âŸ¨y, hyâŸ© := valuation_surjective (uniformizer K)
  rw [â† hy, â† map_zpowâ‚€, â† (ValuativeRel.isEquiv ((valuation L).comap e.toRingHom) _).val_eq] at hn
  simp only [RingEquiv.toRingHom_eq_coe, map_zpowâ‚€, Valuation.comap_apply, RingHom.coe_coe,
    valuation_ringEquiv_eq_uniformizer e hy] at hn
  simp [â† hn]

lemma valuation_ringEquiv (e : K â‰ƒ+* K) (x : K) : valuation K (e x) = valuation K x :=
  (valueGroupWithZeroIsoInt K).injective (valueGroupWithZeroIsoInt_valuation_ringEquiv ..)
